plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'
}

group = 'net.shrimpworks'
version = "1.7"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.env.BUILD_NUMBER}"
} else version += ".DEV"

application {
	mainClass = 'net.shrimpworks.unreal.dependencies.Main'
	mainModule = 'shrimpworks.unreal.dependencies'
}

compileJava {
	options.release = 21
}

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://code.shrimpworks.za.net/artefacts"
	}
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://code.shrimpworks.za.net/artefacts/'
			authentication {
				basic(BasicAuthentication)
			}
			credentials(PasswordCredentials) {
				username = "${System.env.ARTEFACTS_USER}"
				password = "${System.env.ARTEFACTS_PASSWORD}"
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}

dependencies {
	implementation('net.shrimpworks:unreal-package-lib:1.14.30')

	testImplementation('org.junit.jupiter:junit-jupiter:5.14.1')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

jar {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Main-Class': application.mainClass,
		)
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
