import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'jacoco'
}

group = 'net.shrimpworks'
version = "1.0.5"

mainClassName = 'net.shrimpworks.unreal.dependencies.Main'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://code.shrimpworks.za.net/artefacts"
    }
}

publishing {
    repositories {
        maven {
            name = "ShrimpWorks"
            url = 'https://code.shrimpworks.za.net/artefacts/'
            authentication {
                basic(BasicAuthentication)
            }
            credentials(PasswordCredentials) {
                username = "${System.env.ARTEFACTS_USER}"
                password = "${System.env.ARTEFACTS_PASSWORD}"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation 'net.shrimpworks:unreal-package-lib:1.5.50'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': mainClassName,
        )
    }
}

task execJar(type: Jar) {
    archiveClassifier = "exec"
    archiveName = "${baseName}-${classifier}.${extension}"
    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath

    manifest {
        attributes (
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': mainClassName,
                'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
        )
    }

    // build the fat executable jar file
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    // run the script which builds the executable
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        doLast {
            project.exec {
                commandLine(
                        "$projectDir/etc/build-launcher.sh",
                        "$projectDir/etc/launcher.sh",
                        "$destinationDir/$archiveName",
                        "$destinationDir/$baseName")
            }
        }
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}
